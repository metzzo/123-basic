//$$RESETFILE.\UnitTest.gbas
// --------------------------------- //
// Project: UnitTest
// Start: Saturday, December 06, 2014
// IDE Version: 10.283

GLOBAL ExpectedOutput$ = ""
GLOBAL ActualOutput$ = ""


GOSUB TestSimpleVariables
GOSUB TestSimpleIf


KEYWAIT

FUNCTION TEST_OUTPUT: text$
	ExpectedOutput$ = ExpectedOutput$ + text$
ENDFUNCTION

FUNCTION TEST_INPUT: text$
	ActualOutput$ = ActualOutput$  + text$
ENDFUNCTION

FUNCTION TEST_CHECK: name$
	IF ExpectedOutput$ = ActualOutput$
		// ok <3
		STDOUT "Test " + name$ + " OK :) \n"
	ELSE
		STDOUT "Test " + name$ + " Failed :( \n"
		STDOUT "---------- Actual Output ----------\n"
		STDOUT ActualOutput$ + "\n"
		STDOUT "---------- Expected Output --------\n"
		STDOUT ExpectedOutput$ + "\n"
		STDOUT "-----------------------------------\n"
	ENDIF

	ExpectedOutput$ = ""
	ActualOutput$ = ""
ENDFUNCTION

//$$RESETFILE.\Keyword_Function.gbas
// --------------------------------- //
// Project: UnitTest
// Start: Saturday, December 06, 2014
// IDE Version: 10.283


SUB TestSimpleFunctions:
	TEST_INPUT(fibonacci(11))
	TEST_OUTPUT(89)
	TEST_CHECK("Fibonacci")

	TEST_INPUT(recursionTest())
	TEST_OUTPUT(0)
	TEST_CHECK("Simple Recursion")

	TEST_INPUT(Simple$())
	TEST_OUTPUT("Hello World")
	TEST_CHECK("Simple String Return")
ENDSUB


FUNCTION Simple$:
	RETURN "Hello World"
ENDFUNCTION

FUNCTION fibonacci: n
	IF n <= 1
		RETURN n
	ELSE
		RETURN fibonacci(n-1) + fibonacci(n-2)
	ENDIF
ENDFUNCTION

FUNCTION recursionTest: n = 5
	IF n = 0 THEN RETURN
	LOCAL i

	INC i, n
	recursionTest(n - 1)
	DEC i, n

	RETURN i
ENDFUNCTION

//$$RESETFILE.\Keyword_If.gbas
// --------------------------------- //
// Project: UnitTest
// Start: Saturday, December 06, 2014
// IDE Version: 10.283

SUB TestNothing:
	TEST_OUTPUT("0")
	TEST_INPUT("0")
	TEST_CHECK("Nothing")
ENDSUB

SUB TestSimpleIf:
	IF TRUE THEN TEST_INPUT("0")
	IF FALSE THEN TEST_INPUT("1")

	IF TRUE
		TEST_INPUT("2")
	ELSEIF FALSE
		TEST_INPUT("3")
	ELSE
		TEST_INPUT("4")
	ENDIF

	IF FALSE
		TEST_INPUT("5")
	ELSEIF TRUE
		TEST_INPUT("6")
	ELSE
		TEST_INPUT("7")
	ENDIF

	IF FALSE
		TEST_INPUT("8")
	ELSEIF FALSE
		TEST_INPUT("9")
	ELSE
		TEST_INPUT("10")
	ENDIF

	TEST_OUTPUT("02610")
	TEST_CHECK("SimpleIf")
ENDSUB


//$$RESETFILE.\Variables.gbas
// --------------------------------- //
// Project: UnitTest
// Start: Saturday, December 06, 2014
// IDE Version: 10.283


SUB TestSimpleVariables:
	LOCAL i% = 42
	TEST_INPUT(i)
	TEST_OUTPUT("42")
	TEST_CHECK("Simple Integer Declaration")

	LOCAL j = 42.2
	TEST_INPUT(j)
	TEST_OUTPUT("42.2")
	TEST_CHECK("Simple Float Declaration")

	LOCAL k$ = "Hello World"
	TEST_INPUT(k$)
	TEST_OUTPUT("Hello World")
	TEST_CHECK("Simple String Declaration")


	LOCAL calc1% = 10
	LOCAL calc2% = (5 + 6*2) - 100 * calc1
	TEST_INPUT(calc2)
	TEST_OUTPUT("-983")
	TEST_CHECK("Simple Integer Calculation")

	LOCAL calc3 = 10.3
	LOCAL calc4 = (5.1 + 6.2*2.3) - 100.4 * calc1
	TEST_INPUT(calc4)
	TEST_OUTPUT("-984.64")
	TEST_CHECK("Simple Float Calculation")


	LOCAL calc5$ = "Hello"
	LOCAL calc6$ = calc5$ + " World" + 10 +  10.4
	TEST_INPUT(calc6$)
	TEST_OUTPUT("Hello World1010.4")
	TEST_CHECK("Simple String Concatination")
ENDSUB

